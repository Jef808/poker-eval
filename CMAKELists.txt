cmake_minimum_required(VERSION 4.0.1)

project(PokerEval LANGUAGES CXX)

option(BUILD_TESTS "Build the tests" OFF)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

Include(FetchContent)

########
# SFML #
########
FetchContent_Declare(
  SFML
  GIT_REPOSITORY https://github.com/SFML/SFML.git
  GIT_TAG 3.0.1
  GIT_SHALLOW ON
  EXCLUDE_FROM_ALL
  SYSTEM
)
FetchContent_MakeAvailable(SFML)

############
# GUI MAIN #
############
add_executable(holdem_evaluator
  src/main.cpp
  src/draw_card.cpp
  src/card_selector.cpp
  src/reset_button.cpp
  src/utils.cpp
  src/evaluation.cpp
  src/bitset_rankindex.cpp
)

target_include_directories(holdem_evaluator PRIVATE
  ${CMAKE_CURRENT_SOURCE_DIR}/include
)

target_link_libraries(holdem_evaluator PRIVATE
  SFML::Graphics
  SFML::Window
  SFML::System
)

############
# CLI MAIN #
############
add_executable(cli
  src/cli.cpp
  src/utils.cpp
  src/bitset_rankindex.cpp
  src/evaluation.cpp
)

target_include_directories(cli PRIVATE
  ${CMAKE_CURRENT_SOURCE_DIR}/include
)

#########
# tests #
#########
if(BUILD_TESTS)
  FetchContent_Declare(
    Catch2
    GIT_REPOSITORY https://github.com/catchorg/Catch2.git
    GIT_TAG        v3.10.0
  )
  FetchContent_MakeAvailable(Catch2)

  add_executable(tests
    tests/test.cpp
    src/utils.cpp
    src/bitset_rankindex.cpp
  )

  target_include_directories(tests PRIVATE
    ${CMAKE_CURRENT_SOURCE_DIR}/include
  )

  target_link_libraries(tests PRIVATE Catch2::Catch2WithMain)

  list(APPEND CMAKE_MODULE_PATH ${catch2_SOURCE_DIR}/extras)
  include(CTest)
  include(Catch)
  catch_discover_tests(tests)
endif()
